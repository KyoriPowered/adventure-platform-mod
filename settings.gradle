pluginManagement {
  repositories {
    maven {
      name = "stellardriftReleases"
      url = "https://repo.stellardrift.ca/repository/internal/"
      mavenContent { releasesOnly() }
    }
    maven {
      name = "stellardriftSnapshots"
      url = "https://repo.stellardrift.ca/repository/snapshots/"
      mavenContent { snapshotsOnly() }
    }
    mavenLocal {
      mavenContent { snapshotsOnly() }
    }
    mavenCentral()
    maven {
      name = "Fabric"
      url = "https://maven.fabricmc.net"
    }
    gradlePluginPortal()
  }
  
  includeBuild "build-logic"
}

plugins {
  id "fabric-loom" version "0.12.+"
  id "org.spongepowered.gradle.vanilla" version "0.2.1-SNAPSHOT"
  id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.4" apply false
}

dependencyResolutionManagement {
  repositories {
    mavenCentral()
    maven {
      name = "parchment"
      url = "https://maven.parchmentmc.org/"
    }
    maven {
      name = "forge"
      url = "https://maven.minecraftforge.net/"
      mavenContent {
        releasesOnly()
      }
    }
    maven {
      name = "quiltReleases"
      url = "https://maven.quiltmc.org/repository/release/"
      mavenContent {
        includeGroup("org.quiltmc")
        releasesOnly()
      }
    }
  }
}

rootProject.name = "adventure-platform-mod-parent"

["fabric", "forge", "mod-shared"].each {
  include(it)
  findProject(":$it")?.name = "adventure-platform-$it"
}


