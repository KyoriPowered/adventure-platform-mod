import java.nio.file.Files
import net.neoforged.moddevgradle.internal.RunGameTask

plugins {
  // Apply the plugin. You can find the latest version at https://projects.neoforged.net/neoforged/ModDevGradle
  id 'net.neoforged.moddev'
  alias(libs.plugins.indra.checkstyle)
}

neoForge {
  // We currently only support NeoForge versions later than 21.0.x
  // See https://projects.neoforged.net/neoforged/neoforge for the latest updates
  version = "21.0.114-beta"

  // Validate AT files and raise errors when they have invalid targets
  // This option is false by default, but turning it on is recommended
  validateAccessTransformers = true

  runs {
    client {
      client()
      mods.set(new HashSet())
    }
    server {
      server()
      mods.set(new HashSet())
    }
  }

  mods {
    'adventure-platform-neoforge-tester' {
      sourceSet sourceSets.main
    }
  }
}

tasks.withType(RunGameTask).configureEach {
  dependsOn(tasks.jar)
  doFirst {
    def jar = file("run/mods/main.jar")
    jar.parentFile.mkdirs()
    if (jar.exists()) jar.delete()
    Files.copy(tasks.jar.archiveFile.get().asFile.toPath(), jar.toPath())
  }
}

dependencies {
  implementation(project(":adventure-platform-neoforge"))
  jarJar(project(":adventure-platform-neoforge"))
  checkstyle(libs.stylecheck)
}
