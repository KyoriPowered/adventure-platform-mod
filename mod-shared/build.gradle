plugins {
  id 'net.neoforged.moddev'
}

neoForge {
  // Look for versions on https://projects.neoforged.net/neoforged/neoform
  neoFormVersion = "1.21-20240613.152323"

  parchment {
    // Get versions from https://parchmentmc.org/docs/getting-started
    // Omit the "v"-prefix in mappingsVersion
    minecraftVersion = "1.21"
    mappingsVersion = "2024.07.07"
  }
}

dependencies {
  compileOnly("net.fabricmc:sponge-mixin:0.13.4+mixin.0.8.5")
  compileOnly("io.github.llamalad7:mixinextras-common:0.4.0")
  annotationProcessor(libs.autoService)
  annotationProcessor(libs.contractValidator)
  compileOnlyApi(libs.autoService.annotations)

  [
    libs.adventure.key,
    libs.adventure.api,
    libs.adventure.textLoggerSlf4j,
    libs.adventure.textMinimessage,
    libs.adventure.textSerializerPlain,
    libs.adventure.textSerializerAnsi
  ].each {
    api(it) {
      exclude group: "org.slf4j", module: "slf4j-api"
    }
  }

  [
    libs.adventure.platform.api,
    libs.adventure.textSerializerGson
  ].each {
    api(it) {
      exclude group: "com.google.code.gson"
    }
  }
}

sourceSets {
  main {
    java.srcDirs(
      "src/accessor/java",
      "src/mixin/java",
      "src/client/java"
    )
    resources.srcDirs(
      "src/accessor/resources/",
      "src/mixin/resources/",
      "src/client/resources/"
    )
  }
}

tasks.jar {
  manifest.attributes("FMLModType": "GAMELIBRARY")
}
