plugins {
  id 'net.neoforged.moddev'
  id 'publishing-conventions'
}

neoForge {
  neoFormVersion = libs.versions.neoform

  parchment {
    parchmentArtifact = "org.parchmentmc.data:parchment-${libs.versions.parchment.get()}@zip"
  }
}

@CacheableRule
abstract class RemoveAsmConstraint implements ComponentMetadataRule {
  @Override
  void execute(final ComponentMetadataContext ctx) {
    ctx.details.allVariants {
      it.withDependencies { deps ->
        deps.forEach { dep ->
          if (dep.group == 'org.ow2.asm') {
            if (dep.versionConstraint.strictVersion != null) {
              dep.version { it.require(it.strictVersion) }
            }
          }
        }
      }
    }
  }
}

dependencies {
  components {
    // Mojang ships an old version of ASM, which conflicts with our deps. Upstream will probably solve this better when an actual release happens.
    withModule('net.neoforged:minecraft-dependencies', RemoveAsmConstraint)
  }
  compileOnly(libs.mixin)
  compileOnly(libs.mixinExtras)
}

sourceSets {
  main {
    java.srcDirs(
      "src/accessor/java",
      "src/mixin/java",
      "src/client/java"
    )
    resources.srcDirs(
      "src/accessor/resources/",
      "src/mixin/resources/",
      "src/client/resources/"
    )
  }
}

tasks.jar {
  manifest.attributes("FMLModType": "GAMELIBRARY")
}
